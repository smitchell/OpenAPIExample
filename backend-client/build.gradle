plugins {
    id 'org.springframework.boot' version '2.3.3.RELEASE'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
//	https://github.com/asciidoctor/asciidoctor-gradle-plugin
    id 'org.asciidoctor.jvm.convert' version '3.3.0'
    id 'org.asciidoctor.jvm.pdf' version '3.3.0'
//	https://github.com/springdoc/springdoc-openapi-gradle-plugin
    id 'com.github.johnrengelman.processes' version '0.5.0'
    id 'org.springdoc.openapi-gradle-plugin' version '1.3.0'
//	https://github.com/OpenAPITools/openapi-generator/tree/master/modules/openapi-generator-gradle-plugin
	id 'org.openapi.generator' version '5.0.1'
    id "de.undercouch.download" version "4.1.1"
    id 'maven-publish'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'


// ./gradlew build publishToMavenLocal -->  ~/.m2/repository/com/example/backend-client/0.0.1-SNAPSHOT/backend-client-0.0.1-SNAPSHOT.jar

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'maven'
    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
    repositories {
        mavenCentral()
        jcenter()
        mavenLocal()
    }
}

//configurations {
//    [apiElements, runtimeElements].each {
//        it.outgoing.artifacts.removeIf {
//            it.buildDependencies.getDependencies(null).contains(jar)
//        }
//        it.outgoing.artifact(bootJar)
//    }
//}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.data:spring-data-commons'
	implementation 'io.swagger:swagger-core:1.6.2'
    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.1'
    implementation 'javax.validation:validation-api'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
}

test {
    useJUnitPlatform()
}

task downloadFile(type: Download) {
    src "https://raw.githubusercontent.com/smitchell/OpenAPIExample/main/backend-service/src/main/resources/static/docs/openapi.json"
    dest "$buildDir/backend-service-api.json".toString()
    onlyIfModified true
    useETag true
}

// ./gradlew clean openApiGenerate
openApiGenerate {
    generatorName = "spring"
    inputSpec = "$buildDir/backend-service-api.json".toString()
    outputDir = "$projectDir".toString()
    configOptions = [
            useOptional          : "true",
            swaggerDocketConfig  : "false",
            performBeanValidation: "false",
            useBeanValidation    : "false",
            useTags              : "true",
            singleContentTypes   : "true",
            groupId             : "com.example",
            basePackage          : "com.example.backend.client",
            configPackage        : "com.example.backend.client",
            title                : rootProject.name,
            java8                : "false",
            dateLibrary          : "java8",
            serializableModel    : "true",
            artifactId           : rootProject.name,
            apiPackage           : "com.example.backend.client.api",
            modelPackage         : "com.example.backend.client.model",
            invokerPackage       : "com.example.backend.client.api",
            interfaceOnly        : "true"
    ]
}
tasks.openApiGenerate.dependsOn tasks.downloadFile

